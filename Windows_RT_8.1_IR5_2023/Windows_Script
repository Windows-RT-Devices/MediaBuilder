If /i "%CleanupOnly%" == "True" goto SkipUpdateScript

REM ====== Temp - Remove Addons from File Check ========================================================================================================================

If exist "%CD%\BuildPE.cmd" Set FileListEXT=PE

Set MBFileList=%CD%\Includes\Build_FileList%FileListEXT%

If not exist "%MBFileList%" goto SkipMBFileListCorrection

Set /p MBFileListFirstLine=<"%MBFileList%"
If /i not "!MBFileListFirstLine!"=="Addons" goto SkipMBFileListCorrection
Attrib -r -s -h "%MBFileList%" 1> nul

For /f "usebackq tokens=*" %%c in ("%MBFileList%") do (
	Set MBFileListCurrentLine=%%c
	If /i not "!MBFileListCurrentLine:~0,6!"=="Addons" (
		If not "%%c"=="" echo %%c>> "%MBFileList%.tmp"
	)
)

If exist "%MBFileList%.tmp" (
	Del "%MBFileList%" /F /Q 1> nul
	If not exist "%MBFileList%" (
		Rename "%MBFileList%.tmp" "Build_FileList%FileListEXT%" 1> nul
	)
)

:SkipMBFileListCorrection

REM ====== End Temp ====================================================================================================================================================

Set /a MBLatestVersion=!MBLatestVersion!
Set /a MBLatestTestVersion=!MBLatestTestVersion!

If "!WindowsListsURL!" == "" (
	Set ListsURL=!OfficeListsURL!
) else (
	Set ListsURL=!WindowsListsURL!
)

If "!UseTestVersion!" == "True" (
	Set MBUpdateVerStr=v%MBLatestTestVersion:~0,1%.%MBLatestTestVersion:~1,2%
	Set MBUpdateVerNumber=!MBLatestTestVersion!
) else (
	Set MBUpdateVerStr=v%MBLatestVersion:~0,1%.%MBLatestVersion:~1,2%
	Set MBUpdateVerNumber=!MBLatestVersion!
)

If exist "!CD!\Max.Ver" (
	Set /p MBUpdateVerNumberOR=< "!CD!\Max.Ver"
	If !MBUpdateVerNumberOR! LSS !MBLatestVersion! If !MBUpdateVerNumberOR! GTR !MBVerNumber! (
		Set /a MBUpdateVerNumber=!MBUpdateVerNumberOR!
		Set MBUpdateVerStr=v!MBUpdateVerNumberOR:~0,1!.!MBUpdateVerNumberOR:~1,2!
	)
)

If not !MBUpdateVerNumber! GTR !MBVerNumber! (

	If /i "!ForceUpdateArg!" == "True" (
	cls
	echo %white%%ul%________________________________________________________________________________________________________________________%ef%
	echo.
	echo %white%Media Builder Update%ef%
	echo.
	echo 	Installed %MBName%: %white%%MBVer%%ef% %red%^(No Update Found^)%ef%
	echo.
	pause
	)

	goto SkipUpdateScript
)


Set NL=^


Set MBUpdateInfoList=
Set /a MBVerNumberP1 = !MBVerNumber! + 1

For /l %%g in (!MBVerNumberP1!,1,!MBUpdateVerNumber!) do (

	Set MBUpdateInfoText=!TMP!\MB_Update_Info_!random!_!random!
	"%CD%\Bin\wget\%PROCESSOR_ARCHITECTURE%\wget.exe" -O "!MBUpdateInfoText!" "!ListsURL!_Update_%%g" -nc --timeout=2 --read-timeout=2 --tries=2 --no-check-certificate -q --show-progress --progress=bar:force:noscroll >nul 2>&1

	If "!ErrorLevel!" EQU "0" (

		If %%g EQU !MBVerNumberP1! Set MBUpdateInfoList=!MBUpdateInfoList!;"An update for %MBName% %ul%%white%%MBVer%%ef% to %ul%%green%%MBUpdateVerStr%%ef% is now available.!NL!^"

		If %%g EQU !MBVerNumberP1! Set MBUpdateInfoList=!MBUpdateInfoList!;"%white%	Updates:%ef%"

		Set MBUpdateListVerNumber=%%g
		Set MBUpdateListVerStr=v!MBUpdateListVerNumber:~0,1!.!MBUpdateListVerNumber:~1,2!
		
		Set MBUpdateDateSet=False
		For /f "usebackq skip=1 tokens=* delims=" %%d in ("!MBUpdateInfoText!") do (
			If /i not "!MBUpdateDateSet!" == "True" Set MBUpdateDate=%%d
			Set MBUpdateDateSet=True
		)

		Set MBUpdateInfoList=!MBUpdateInfoList!;"%green%	!MBUpdateListVerStr! (!MBUpdateDate!)%ef%"

		For /f "usebackq skip=2 tokens=* delims=" %%a in ("!MBUpdateInfoText!") do (
			Set MBUpdateInfoList=!MBUpdateInfoList!;"	%%a"
		)

		If %%g EQU !MBUpdateVerNumber! (
			Set MBUpdateInfoList=!MBUpdateInfoList!;"!NL!^%white%	Update Notes:%ef%"
			Set MBUpdateInfoList=!MBUpdateInfoList!;"	- Installing an update will cause any custom settings to be reset."
		)

		Del "!MBUpdateInfoText!" /F /Q >nul 2>&1
	) else (
		goto SkipUpdateScript
	)

)

If exist "%CD%\UpgradeInfo.cmd" call "%CD%\UpgradeInfo.cmd"

:UpdateScriptMenu
cls
rem echo %white%%ul%________________________________________________________________________________________________________________________%ef%
rem echo.
echo %white%Media Builder Update%ef%
echo.
For %%a in (!MBUpdateInfoList!) do (
	echo %%~a
)
echo.
Set MBUpdateConfirmPrompt=
Set /p MBUpdateConfirmPrompt=%white%Would you like to update Media Builder (Y/N)? %ef%
If /i "!MBUpdateConfirmPrompt!"=="Y" Set MBUpdateConfirm=True
If /i "!MBUpdateConfirmPrompt!"=="N" Set MBUpdateConfirm=False
If "%MBUpdateConfirm%" == "" goto UpdateScriptMenu
echo %ul%________________________________________________________________________________________________________________________%ef%
If "%MBUpdateConfirm%" == "False" (
	cls
	echo Please wait...
	goto SkipUpdateScript
)
echo.

Set MBUpdateZip=!TMP!\MB_Update_!MBUpdateVerNumber!_!random!_!random!.zip
"%CD%\Bin\wget\%PROCESSOR_ARCHITECTURE%\wget.exe" -O "!MBUpdateZip!" "!ListsURL!_Update_!MBUpdateVerNumber!.zip" --timeout=2 --read-timeout=2 --tries=2  -nc --no-check-certificate -q --show-progress --progress=bar:force:noscroll
If "!ErrorLevel!" EQU "0" (
	echo.
	Set MBUpdateInstall=!TMP!\MB_Update_!MBUpdateVerNumber!_Install_!random!_!random!.cmd
	(
		echo @echo off
		echo @setlocal enableextensions
		echo @setlocal EnableDelayedExpansion 
		echo @cd /d "%%~dp0"
		echo Set MediaBuilderPath=!CD!
		echo Set MBUpdateZipPath=!MBUpdateZip!
		echo Set MBUpdateTitle=!MBName! Update Script
		echo @Title ^^!MBUpdateTitle^^!
		echo.
		echo echo Running ^^!MBUpdateTitle^^!.
		echo echo Please wait...
		echo timeout /t 10 /nobreak
		echo "^!MediaBuilderPath^!\Bin\7z\%%PROCESSOR_ARCHITECTURE%%\7z.exe" x "^!MBUpdateZipPath^!" -aoa -o"^!MediaBuilderPath^!"
		echo echo.
		echo If "^!ErrorLevel^!" NEQ "0" ^(
		echo 	echo Error: Unable to extract updates.
		echo	echo.
		echo 	pause
		echo 	exit
		echo ^) else ^(
		echo 	echo Extracted updates successfully.
		echo 	If exist "^!MediaBuilderPath^!\Config\Build_Config.cfg" del "^!MediaBuilderPath^!\Config\Build_Config.cfg" /f /q ^>nul 2^>^&1
		echo ^)
		echo echo.
		echo If exist "^!MediaBuilderPath^!\CleanupFiles.lst" ^(
		echo 	For /f "usebackq tokens=*" %%%%f in ^("^!MediaBuilderPath^!\CleanupFiles.lst"^) do ^(
		echo			rem echo Deleting File: "^!MediaBuilderPath^!\%%%%f"
		echo			If exist "^!MediaBuilderPath^!\%%%%f" attrib -r -s -h "^!MediaBuilderPath^!\%%%%f" ^>nul 2^>^&1
		echo			If exist "^!MediaBuilderPath^!\%%%%f" del "^!MediaBuilderPath^!\%%%%f" /f /q ^>nul 2^>^&1
		echo 	^)
		echo 	If exist "^!MediaBuilderPath^!\CleanupFiles.lst" del "^!MediaBuilderPath^!\CleanupFiles.lst" /f /q ^>nul 2^>^&1
		echo ^)
		echo If exist "^!MediaBuilderPath^!\CleanupDir.lst" ^(
		echo 	For /f "usebackq tokens=*" %%%%d in ^("^!MediaBuilderPath^!\CleanupDir.lst"^) do ^(
		echo			rem echo Deleting Directory: "^!MediaBuilderPath^!\%%%%d"
		echo			attrib -s -h -r /s /d "^!MediaBuilderPath^!\%%%%d" ^>nul 2^>^&1
		echo			If exist "^!MediaBuilderPath^!\%%%%d" rmdir /Q /S "^!MediaBuilderPath^!\%%%%d" ^>nul 2^>^&1
		echo 	^)
		echo 	If exist "^!MediaBuilderPath^!\CleanupDir.lst" del "^!MediaBuilderPath^!\CleanupDir.lst" /f /q ^>nul 2^>^&1
		echo ^)
		echo echo Launching Media Builder, please wait...
		echo timeout /t 4 /nobreak ^> nul
		echo If exist "^!MediaBuilderPath^!\Build.cmd" Set LaunchMB=Build.cmd
		echo If exist "^!MediaBuilderPath^!\BuildPE.cmd" Set LaunchMB=BuildPE.cmd
		echo start /i "" powershell.exe Start-Process '^^!MediaBuilderPath^^!\^^!LaunchMB^^!' -Verb runas
		echo If exist "^!MBUpdateZipPath^!" del "^!MBUpdateZipPath^!" /f /q ^>nul 2^>^&1
		echo ^(goto^) 2^>nul ^& If exist "%%~f0" del "%%~f0" /f /q ^>nul 2^>^&1
		echo exit
	) > "!MBUpdateInstall!"
	If exist "!MBUpdateInstall!" (
		echo %white%Launching update script, please wait...%ef%
		timeout /t 4 /nobreak > nul
		start /i "" powershell.exe Start-Process '!MBUpdateInstall!' -Verb runas
		Set MBScriptExit=True
		goto SkipUpdateScript
	) else (
		echo %red%Error: Unable to launch update script.%ef%
		echo.
		pause
		goto SkipUpdateScript
	)
) else (
	echo %red%Error: Unable to download update.%ef%
	echo %red%Wget Error: !ErrorLevel!%ef%
	echo.
	pause
	goto SkipUpdateScript
)

:SkipUpdateScript
